group "com.exa.pesa"
version "1.0.0-SNAPSHOT"

buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
        classpath("io.spring.gradle:propdeps-plugin:0.0.9.RELEASE")
    }
}

apply plugin: "java"
apply plugin: "org.springframework.boot"
configure(allprojects) {
    apply plugin: "propdeps"
}

//TASKS
task copyAngularDist(type: Copy) {
    from "angular-ui/dist"
    include("**")
    into "$buildDir/resources/main/static"
}

//JAVA PLUGIN
jar {
    baseName = "exa-pesa-core"
    version = "1.0.0"
}

processResources {
    dependsOn copyAngularDist
    if (!project.hasProperty('no-expand')) {
        expand(project.properties)
    }
}

compileJava.dependsOn(processResources)

sourceCompatibility = 1.8
targetCompatibility = 1.8

//SPRING BOOT PLUGIN
bootRepackage {
    mainClass = "com.exa.pesa.core.Main"
}

springBoot {
    executable = true
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("com.microsoft.sqlserver:mssql-jdbc:6.2.1.jre8")

    optional "org.springframework.boot:spring-boot-configuration-processor"

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("junit:junit:4.12")
}